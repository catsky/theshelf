class Search
  constructor: (el) ->
    @$el = $(el)
    @searchBooks()
    @bindSearch()

  bindSearch: ->
    @$el.keyup _.debounce(
      => @searchBooks()
      200)

  searchBooks: ->
    search_url = "<%= Rails.application.routes.url_helpers.api_v1_search_path %>"
    $.ajax search_url,
      dataType: "json"
      type: "GET"
      data:
        search_box: @$el.val()
      success: (data) =>
        dataJson = data.map (item) ->
          JSON.parse item
        new UpdatesBookGallery($('#book-list'), dataJson).update()

class UpdatesBookGallery
  constructor: (el, data) ->
    @$el = $(el)
    @$data = data
    @clearSearchResults()

  clearSearchResults: ->
    @$el.empty()

  update: ->
    template = JST["book"]
    bookArray = []
    _.each @$data, (item) =>
      bookArray.push _.template(template(item))().trim()
    @$el.html bookArray.join('')

$.fn.search = ->
  new Search(this)
