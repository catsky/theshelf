class Rating
  constructor: (el) ->
    @$el = $(el)
    @setCurrentRating()
    @bindElements()

  setCurrentRating: ->
    rating = @$el.data('rating')
    @$el.find("#rating_#{rating}").prop('checked', true)

  toggleLoading: ->
    $('#my-rating-form').children().toggleClass('hide-visually')

  rate: ->
    $('input[name="rating"]').on 'change', (event) =>
      @toggleLoading()
      id = @$el.data('bookid')
      rating = if event.currentTarget.checked is true then event.currentTarget.value else "0"
      $.ajax '/api/v1/rate',
        type: "PATCH"
        data:
          book_id: id
          rating: rating
        success: =>
          new UpdatesAverage('#avg-rating', id).update()
          @toggleLoading()

  bindElements: ->
    @rate()
    new ClearsRating('#my-rating')

class UpdatesAverage
  constructor: (el, bookId) ->
    @$el = $(el)
    @$bookId = bookId

  update: ->
    $.ajax '/api/v1/average',
      type: 'GET'
      data:
        book_id: @$bookId
      success: (data) =>
        @$el.html(data.attachmentPartial)

class ClearsRating
  constructor: (el) ->
    @$el = $(el)
    @bindClearRating()
    @toggleClearRating()

  bindClearRating: ->
    $('#clear-rating').click ->
      $('input[name="rating"]:checked').prop('checked', false).change()

  toggleClearRating: ->
    @$el.hover =>
      $('#my-rating-message').children().toggleClass('hide-visually')


$.fn.rating = ->
  new Rating(this)
